apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = "io.innerloop"
version = "0.3.0"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.neo4j.app', name: 'neo4j-server', version: '2.2.1'
    testCompile group: 'org.neo4j', name: 'neo4j-io', version: '2.2.1', classifier: 'tests'
    testCompile group: 'org.neo4j.app', name: 'neo4j-server', version: '2.2.1', classifier: 'tests'
    testCompile group: 'org.neo4j', name: 'neo4j-kernel', version: '2.2.1', classifier: 'tests'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getProperty('nexusUsername'), password: System.getProperty('nexusPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getProperty('nexusUsername'), password: System.getProperty('nexusPassword'))
            }

            pom.project {
                name project.name
                packaging 'jar'
                description 'A compact Java driver that supports graph formats for standalone Neo4J instances.'
                url 'https://github.com/inner-loop/java-neo4j-client'

                scm {
                    connection 'git@github.com:inner-loop/java-neo4j-client.git'
                    developerConnection 'git@github.com:inner-loop/java-neo4j-client.git'
                    url 'https://github.com/inner-loop/java-neo4j-client'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE, Version 2.0'
                        url 'http://www.gnu.org/licenses/gpl-2.0.html'
                    }
                }

                developers {
                    developer {
                        id 'mangrish'
                        name 'Mark Angrish'
                        email 'mark.angrish@innerloop.io'
                    }
                }
            }
        }
    }
}

